plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.8.7'
}

repositories {
    maven { url = "https://maven.terraformersmc.com" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.ladysnake.org/releases" }
    maven { url = "https://maven.gegy.dev/" }
    maven { url = "https://mvn.devos.one/snapshots/" }
    maven { url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { url = "https://maven.florens.be/releases" }
    maven { url = "https://maven.parchmentmc.org" }
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://jitpack.io"
        content {
            includeGroupByRegex "com.github.*"
        }
    }
    maven { url "https://maven.bai.lol" }
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
    }
    maven { url = 'https://maven.minecraftforge.net/' }
}

allprojects { repositories { maven { url 'https://jitpack.io' } } }

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version + '+' + project.minecraft_version
group = project.maven_group

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

loom {
    runs {
        datagen {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${project.archives_base_name}"

            runDir "build/datagen"
        }
    }

    accessWidenerPath = file("src/main/resources/terramine.accesswidener")
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Dependencies
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}+${project.minecraft_version}"
    modImplementation "curse.maven:architectury-api-419699:${project.architectury_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${project.minecraft_version}-${project.terrablender_version}"
    // modImplementation "software.bernie.geckolib:geckolib-fabric-1.19:${project.geckolib_version}"
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-level:${project.cca_version}")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}")
    modImplementation include("be.florens:expandability-fabric:${project.expandability_version}")
    modImplementation include("io.github.cottonmc:LibGui:${project.libgui_version}+${project.minecraft_version}")
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}")))

    // Optional
    modImplementation("com.terraformersmc:modmenu:${project.mod_menu_version}") {
        transitive = false
    }
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    // !!!!!!!!!!!! not yet updated to 1.20.6, uncomment once updated !!!!!!!!!!!
    //modImplementation("dev.lambdaurora:lambdynamiclights:${project.lamb_dynamic_lights_version}+${project.minecraft_version}")

    //modImplementation "curse.maven:ftb-teams-fabric-438497:${project.ftb_teams_version}"
    //modImplementation "curse.maven:ftb-library-fabric-438495:${project.ftb_library_version}"

    // Compat
    modCompileOnly("com.github.apace100:origins-fabric:v${project.origins_version}") {
        transitive = false
    }
    modCompileOnly("com.github.apace100:apoli:v${project.apoli_version}") {
        transitive = false
    }
    modCompileOnly("com.github.apace100:calio:v${project.calio_version}") {
        transitive = false
    }
    modCompileOnly("curse.maven:haema-391257:${project.haema_version}") {
        transitive = false
    }
    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from file('src/main/generated')
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    dependsOn 'runDatagen'
}

curseforge {
    if (System.getenv("CURSE_API_KEY") != null) {
        apiKey = System.getenv("CURSE_API_KEY")

        project {
            id = "629183"
            releaseType = "beta"

            changelogType = "markdown"
            changelog = System.getenv("CHANGELOG") ?: "https://github.com/jakeb072001/TerraMine/blob/" + project.minecraft_version + "/CHANGELOG.md"

            addGameVersion project.minecraft_version
            addGameVersion "Fabric"
            relations {
                requiredDependency "architectury-api"
                requiredDependency "cloth-config"
                requiredDependency "fabric-api"
                requiredDependency "terrablender-fabric"
                optionalDependency "continuity"
                optionalDependency "lambdabettergrass"
                optionalDependency "lambdynamiclights"
            }

            mainArtifact(remapJar.archiveFile) {
                displayName = "[" + project.minecraft_version + "] " + "TerraMine " + project.mod_version
            }

            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "terramine"
    versionName = "[" + project.minecraft_version + "] " + "TerraMine " + project.mod_version
    versionNumber = project.mod_version
    versionType = "beta"
    uploadFile = remapJar
    changelog = System.getenv("CHANGELOG") ?: "https://github.com/jakeb072001/TerraMine/blob/" + project.minecraft_version + "/CHANGELOG.md"

    dependencies {
        required.project "fabric-api"
        required.project "cloth-config"
        required.project "architectury-api"
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
